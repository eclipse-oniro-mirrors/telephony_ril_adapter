# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/telephony/core_service/telephony.gni")
import("//build/ohos.gni")
import("//drivers/adapter/uhdf2/uhdf.gni")

RIL_ADAPTER = "//base/telephony"
RIL_FRAMEWORKS = "//base/telephony/ril_adapter/frameworks/src"

config("telephony_sim_config") {
  if (defined(sim_slot_num)) {
    defines = [ "SIM_SLOT_NUM = $sim_slot_num" ]
    if (sim_slot_num > 1) {
      defines += [ "MULTI_SIM_SLOT_SUPPORT" ]
    }
  } else {
    defines = [ "SIM_SLOT_NUM = 1" ]
  }
}

config("hril_config") {
  # header file path
  include_dirs = [ "$RIL_ADAPTER/ril_adapter/interfaces/innerkits/include" ]

  cflags = []
  if (is_double_framework) {
    cflags = [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (is_standard_system) {
    cflags += [ "-DCONFIG_STANDARD_SYSTEM" ]
  }
  if (defined(build_public_version) && build_public_version) {
    cflags += [ "-DBUILD_PUBLIC_VERSION" ]
  }
}

ohos_shared_library("hril_innerkits") {
  configs = [ ":telephony_sim_config" ]
  sources = [
    "$RIL_FRAMEWORKS/hril_base_parcel.cpp",
    "$RIL_FRAMEWORKS/hril_call_parcel.cpp",
    "$RIL_FRAMEWORKS/hril_data_parcel.cpp",
    "$RIL_FRAMEWORKS/hril_modem_parcel.cpp",
    "$RIL_FRAMEWORKS/hril_network_parcel.cpp",
    "$RIL_FRAMEWORKS/hril_sim_parcel.cpp",
    "$RIL_FRAMEWORKS/hril_sms_parcel.cpp",
  ]
  include_dirs = [
    "$RIL_ADAPTER/ril_adapter/services/hril/include",
    "//drivers/adapter/uhdf2/include/hdi",
    "$RIL_ADAPTER/ril_adapter/interfaces/innerkits/include",
    "//third_party/json/include",
    "include",
  ]
  public_configs = [ ":hril_config" ]
  defines = [
    "TELEPHONY_LOG_TAG = \"Hril\"",
    "LOG_DOMAIN = 0xD002B01",
  ]
  deps = [
    "//base/telephony/core_service/utils:libtelephony_common",
    "//drivers/adapter/uhdf2/host:libhdf_host",
    "//drivers/adapter/uhdf2/ipc:libhdf_ipc_adapter",
    "//drivers/adapter/uhdf2/utils:libhdf_utils",
  ]
  external_deps = [ "ipc:ipc_core" ]

  public_deps = [ "//utils/native/base:utils" ]

  if (is_standard_system) {
    external_deps += [ "hiviewdfx_hilog_native:libhilog" ]
  } else {
    external_deps += [ "hilog:libhilog" ]
  }

  part_name = "ril_adapter"
  subsystem_name = "telephony"
}
