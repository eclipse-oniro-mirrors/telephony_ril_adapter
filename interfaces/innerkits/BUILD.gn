# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

RIL_ADAPTER = "//base/telephony/ril_adapter"
RIL_FRAMEWORKS = "//base/telephony/ril_adapter/frameworks/src"

config("hril_config") {
  include_dirs = [ "$RIL_ADAPTER/interfaces/innerkits/include" ]

  cflags = []
  if (is_double_framework) {
    cflags = [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (is_standard_system) {
    cflags += [ "-DCONFIG_STANDARD_SYSTEM" ]
  }
  if (defined(build_public_version) && build_public_version) {
    cflags += [ "-DBUILD_PUBLIC_VERSION" ]
  }
}

ohos_shared_library("hril_innerkits") {
  all_dependent_configs = [
    ":hril_config",
    "$RIL_ADAPTER/utils:utils_config",
  ]

  sources = [ "$RIL_FRAMEWORKS/hril_base_parcel.cpp" ]

  public_configs = [
    ":hril_config",
    "$RIL_ADAPTER/utils:utils_config",
  ]

  defines = [
    "TELEPHONY_LOG_TAG = \"HrilInnerkits\"",
    "LOG_DOMAIN = 0xD001F08",
  ]

  external_deps = [ "c_utils:utils" ]

  if (is_standard_system) {
    external_deps += [ "hiviewdfx_hilog_native:libhilog" ]
  } else {
    external_deps += [ "hilog:libhilog" ]
  }
  relative_install_dir = "chipset-pub-sdk"

  part_name = "drivers_interface_ril"
  subsystem_name = "telephony"
}
